<!DOCTYPE html>
<html lang="en">
	<head>
		<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
		<title>WebGL Water-Ripples-2D</title>
		<style>
			#attribution {
				background-color: #dedede;
				border: 1px solid #aaa;
				border-radius: 2px;
				bottom: 10px;
				box-shadow: 2px 2px 2px 0px #bbb;
				height: 15px;
				padding: 5px;
				position: absolute;
				right: 10px;
				text-align: right;
				width: 530px;
			}
			#canvas {
				border: 0;
				left: 0;
				position: absolute;
				top: 0;
			}
			#container {
				width: 100%;
			}
			#controls {
				background-color: #eee;
				border: 1px solid #ccc;
				border-radius: 2px;
				box-shadow: 2px 2px 2px 0px #bbb;
				margin-left: 980px;
				padding: 5px;
				width: 210px;
			}
			#hud {
				border: 0;
				pointer-events: none;
				left: 0;
				position: absolute;
				top: 0;
				z-index: 2;
			}
			#view {
				padding-left: 5px;
				padding-top: 5px;
				position: absolute;
				width: 960px;
			}
			input {
				vertical-align: middle;
			}
		</style>
	</head>

	<body>
		<a href="https://github.com/Dave13h/WebGL-Water-Ripples-2D/"><img
			style="position: absolute; top: 0; right: 0; border: 0;" alt="Fork me on GitHub"
			src="https://camo.githubusercontent.com/38ef81f8aca64bb9a64448d0d70f1308ef5341ab/68747470733a2f2f73332e616d617a6f6e6177732e636f6d2f6769746875622f726962626f6e732f666f726b6d655f72696768745f6461726b626c75655f3132313632312e706e67"
			data-canonical-src="https://s3.amazonaws.com/github/ribbons/forkme_right_darkblue_121621.png"></a>

		<!-- Something to draw on -->
		<div id="container">
			<div id="view">
				<canvas id="canvas" width="960px" height="540px"></canvas>
				<canvas id="hud" width="960px" height="540px"></canvas>
			</div>

			<div id="controls">
				<label for="damping">Damping</label><br>
				<input type="range" id="damping" min="0" max="10" step="0.01" value="9.88">
				<input type="text" size="5" value="0.988" id="d_val"></input><br><br>

				<label for="rate">Rate</label><br>
				<input type="range" id="rate" min="0" max="100" step="1" value="50"> 
				<input type="text" size="5" value="50" id="r_val"></input>
			</div>
		</div>

		<div id="attribution">
			<a href="https://github.com/aethersis/water-ripples-2D">Water-Ripples-2D
				by Aethersis</a> -
			<a href="http://glmatrix.net/" target="_blank">glMatrix.js</a> -
			<a href="http://opengameart.org/content/brick-wall">Brick wall
				texture by JosipKladaric</a>
		</div>

		<!-- Floor Shader -->
		<script id="floor-vs" type="x-shader/x-vertex">
			attribute vec3 aPos;
			attribute vec2 aUV;
			attribute vec3 aNormal;
			attribute vec3 aTangent;
			attribute vec3 aBiTangent;

			uniform mat4 uCamMat;
			uniform mat4 uPMat;

			uniform mat4 uMat;

			varying mat3 vTBN;
			varying vec4 vPosition;
			varying vec2 vUV;

			void main(void) {
				vUV = aUV;

				vec3 n = vec3(normalize(uMat * vec4(aNormal, 	0.0)));
				vec3 b = vec3(normalize(uMat * vec4(aBiTangent, 0.0)));
				vec3 t = vec3(normalize(uMat * vec4(aTangent,   0.0)));
				vTBN = mat3(t, b, n);

				vPosition = uMat * vec4(aPos, 1.0);
				gl_Position = uPMat * uCamMat * uMat * vPosition;
			}
		</script>
		<script id="floor-fs" type="x-shader/x-fragment">
			precision highp float;

			uniform sampler2D tAlbedo;
			uniform sampler2D tNormal;
			uniform sampler2D tSpec;
			uniform sampler2D tRain;
			uniform float tRainSize;

			uniform vec3 uCamPos;

			uniform vec3 lAmbientColour;
			uniform vec3 lPointColour;
			uniform vec3 lPointPosition;
			uniform float lPointAttn;
			uniform float lPointIntensity;

			varying mat3 vTBN;
			varying vec4 vPosition;
			varying vec2 vUV;

			vec3 heightmapNormal(void) {
				float n = texture2D(tRain, vec2(vUV.x, vUV.y + tRainSize)).r / 100.0;
				float s = texture2D(tRain, vec2(vUV.x, vUV.y - tRainSize)).r / 100.0;
				float e = texture2D(tRain, vec2(vUV.x + tRainSize, vUV.y)).r / 100.0;
				float w = texture2D(tRain, vec2(vUV.x - tRainSize, vUV.y)).r / 100.0;

				vec3 noma = normalize(vec3(tRainSize, s-n, 0.0));
				vec3 nomb = normalize(vec3(0.0, e-w, tRainSize));
				return cross(noma, nomb);
			}

			void main(void) {
				vec4 albedo = texture2D(tAlbedo, vUV);
				vec3 normal = (texture2D(tNormal, vUV).rgb * 2.0 - 1.0);
				float spec  = texture2D(tSpec, vUV).r * 255.0;

				// Flip the Y channel
				normal.g = 1.0 - normal.g;

				// Heightmap from the rain
				normal += heightmapNormal();
				normal = normalize(normal);
				normal = normalize(vTBN * normal);

				// Ambient
				vec3 lighting = lAmbientColour;

				// Diffuse
				vec3 lDir = normalize(lPointPosition - vPosition.xyz);
				float lDist = length(lPointPosition - vPosition.xyz);
				float lAttn = 1.0 / (1.0 + lPointAttn * pow(lDist, 2.0));
				float diffuse = max(0.0, dot(lDir, normal));
				lighting += lAttn * diffuse * (lPointColour * lPointIntensity);

				// Specular
				vec3 rDir = normalize(-reflect(lDir, normal));
				vec3 eDir = normalize(-uCamPos - vPosition.xyz);
				float specular = pow(max(0.0, dot(rDir, eDir)), spec);
				lighting += lAttn * specular * (lPointColour * lPointIntensity);

				// Gamma Correct
				vec4 outColour = vec4(lighting * albedo.rgb, 1.0);
				gl_FragColor = vec4(pow(outColour.rgb, vec3(1.0 / 2.2)), outColour.a);
			}
		</script>

		<!-- Rain Sim Shader -->
		<script id="rainSim-vs" type="x-shader/x-vertex">
			attribute vec2 aPos;
			//attribute vec2 aUV;

			uniform vec2 uOffset;

			varying float vR;
			varying vec2 vPos;
			varying vec2 vLPos;

			void main(void) {
				vLPos = aPos; // Local position
				vPos = aPos + uOffset;
				gl_Position = vec4(vPos, 0.0, 1.0);
			}
		</script>
		<script id="rainSim-fs" type="x-shader/x-fragment">
			precision highp float;

			#define PI 3.31415926535

			uniform sampler2D tCurrent;
			uniform float uStrength;

			uniform vec2 uMid;
			uniform vec2 uPOffset;
			varying vec2 vLPos;

			varying vec2 vPos;
			varying float vR;

			void main(void) {
				float cur = texture2D(tCurrent, vPos).r;

				// Mid hit point
				vec2 dFromMid = vec2(
					uPOffset.x - vLPos.x,
					uPOffset.y - vLPos.y);

				vec2 pixelPos = vec2(
					514.0 * dFromMid.x,
					514.0 * dFromMid.y
				);

				float r = sqrt(pow(pixelPos.x, 2.0) + pow(pixelPos.y, 2.0));

				float sim = cur + -uStrength * sin(PI * r * 0.32) / (25.0 * r + 0.001);
				gl_FragColor = vec4(sim, sim, sim, 1.0);
			}
		</script>

		<!-- Propagate Shader -->
		<script id="propagate-vs" type="x-shader/x-vertex">
			attribute vec3 aPos;
			attribute vec2 aUV;
			varying vec2 vUV;

			void main(void) {
				vUV = aUV;
				gl_Position = vec4(aPos, 1.0);
			}
		</script>
		<script id="propagate-fs" type="x-shader/x-fragment">
			precision highp float;

			uniform sampler2D tPrev;
			uniform sampler2D tCurrent;
			uniform float uSize;
			uniform float uC;

			varying vec2 vUV;

			float neumann(vec2 offset) {
				return 
					texture2D(tCurrent, vec2(offset.s + uSize, offset.t)).r +
					texture2D(tCurrent, vec2(offset.s - uSize, offset.t)).r + 
					texture2D(tCurrent, vec2(offset.s, offset.t - uSize)).r + 
					texture2D(tCurrent, vec2(offset.s, offset.t + uSize)).r;
			}

			void main(void) {
				float current = texture2D(tCurrent, vUV).r;
				float prev = texture2D(tPrev, vUV).r;
				float prop = uC * (neumann(vUV) - 4.0 * current) - prev + 2.0 * current;
				gl_FragColor = vec4(prop, prop, prop, 1.0);
			}
		</script>

		<!-- Damping Shader -->
		<script id="damping-vs" type="x-shader/x-vertex">
			attribute vec3 aPos;
			attribute vec2 aUV;

			varying vec2 vUV;

			void main(void) {
				vUV = aUV;
				gl_Position = vec4(aPos, 1.0);
			}
		</script>
		<script id="damping-fs" type="x-shader/x-fragment">
			precision highp float;

			uniform sampler2D tTexture;
			uniform float uDamping;

			varying vec2 vUV;

			void main(void) {
				gl_FragColor = vec4(texture2D(tTexture, vUV).rgb * uDamping, 1.0);
			}
		</script>

		<!-- Copy Texture Shader -->
		<script id="copyTexture-vs" type="x-shader/x-vertex">
			attribute vec3 aPos;
			attribute vec2 aUV;

			varying vec2 vUV;

			void main(void) {
				vUV = aUV;
				gl_Position = vec4(aPos, 1.0);
			}
		</script>
		<script id="copyTexture-fs" type="x-shader/x-fragment">
			precision highp float;

			uniform sampler2D tTexture;
			varying vec2 vUV;

			void main(void) {
				gl_FragColor = texture2D(tTexture, vUV);
			}
		</script>

		<!-- TextureViewer Shader -->
		<script id="textureViewer-vs" type="x-shader/x-vertex">
			attribute vec3 aPos;
			attribute vec2 aUV;

			uniform vec2 uOffset;
			uniform vec2 uSize;
			uniform float uWinHeight;
			uniform float uWinWidth;

			varying vec2 vUV;

			void main(void) {
				vUV = aUV;
				
				vec3 pos = aPos;
				pos.x += uOffset.x + (pos.x * uSize.x);
				pos.y += uOffset.y + (pos.y * uSize.y);
				pos.xy -= vec2(uWinWidth / 2.0, uWinHeight / 2.0);
				pos.xy /= vec2(uWinWidth / 2.0, uWinHeight / 2.0);
				gl_Position = vec4(pos, 1.0);
			}
		</script>
		<script id="textureViewer-fs" type="x-shader/x-fragment">
			precision highp float;

			uniform sampler2D tTexture;
			
			varying vec2 vUV;

			void main(void) {
				gl_FragColor = texture2D(tTexture, vUV);
			}
		</script>

		<!-- External Libs -->
		<script src="scripts/gl-matrix-min.js"></script>

		<!-- pff, what autoloader? -->
		<script src="scripts/camera.js"></script>
		<script src="scripts/framebuffer.js"></script>
		<script src="scripts/hud.js"></script>
		<script src="scripts/input.js"></script>
		<script src="scripts/rain.js"></script>
		<script src="scripts/shader.js"></script>
		<script src="scripts/texture.js"></script>
		<script src="scripts/scene.js"></script>

		<!-- Include last ;) -->
		<script src="scripts/main.js"></script>
	</script>
</html>
